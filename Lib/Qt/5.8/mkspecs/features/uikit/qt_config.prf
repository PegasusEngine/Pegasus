load(qt_config)

isEmpty(QT_ARCH) {
    # The configure tests are run without QT_ARCH being resolved yet, which
    # means we fail to pass -arch to the compiler, resulting in broke tests.
    # As the Xcode toolchain doesn't seem to have a way to auto-detect the
    # arch based on the SDK, we have to hard-code the arch for configure.
    contains(QMAKE_MAC_SDK, $${device.sdk}.*) {
        QT_ARCH = arm
    } else { # Simulator
        ios: QT_ARCH = i386
        tvos: QT_ARCH = x64
        watchos: QT_ARCH = i386
    }

    # Prevent the arch/config tests from building as multi-arch binaries,
    # as we only want the lowest common denominator features.
    CONFIG += single_arch
}
