#
# qmake configuration for the Jetson TK1 Pro boards running Vibrante Linux
#
# This environment supports eglfs (via DRM, EGLDevice, and EGLStream),
# Wayland (requires an EGLStream-capable compositor), and X11 (untested).
#
# A typical configure line might look like:
# configure \
#   -device jetson-tk1-pro \
#   -device-option VIBRANTE_SDK_TOPDIR=/opt/nvidia/vibrante-vcm30t124-linux
#   -device-option CROSS_COMPILE=/opt/nvidia/toolchains/tegra-4.8.1-nv/usr/bin/arm-cortex_a15-linux-gnueabi/arm-cortex_a15-linux-gnueabi- \
#   -sysroot /opt/nvidia/vibrante-vcm30t124-linux/targetfs \
#   -no-gcc-sysroot

include(../common/linux_device_pre.conf)

isEmpty(VIBRANTE_SDK_TOPDIR):error("You must pass -device-option VIBRANTE_SDK_TOPDIR=/path/to/sdk")

QMAKE_INCDIR += \
    $${VIBRANTE_SDK_TOPDIR}/include \
    $$[QT_SYSROOT]/usr/include

QMAKE_LIBDIR += \
    $${VIBRANTE_SDK_TOPDIR}/lib-target \
    $$[QT_SYSROOT]/usr/lib \
    $$[QT_SYSROOT]/lib/arm-linux-gnueabihf \
    $$[QT_SYSROOT]/usr/lib/arm-linux-gnueabihf

QMAKE_LFLAGS += \
    -Wl,-rpath-link,$${VIBRANTE_SDK_TOPDIR}/lib-target \
    -Wl,-rpath-link,$$[QT_SYSROOT]/usr/lib \
    -Wl,-rpath-link,$$[QT_SYSROOT]/usr/lib/arm-linux-gnueabihf \
    -Wl,-rpath-link,$$[QT_SYSROOT]/lib/arm-linux-gnueabihf

DISTRO_OPTS                  += hard-float
COMPILER_FLAGS               += -mtune=cortex-a15 -march=armv7-a -mfpu=neon-vfpv4 -DWIN_INTERFACE_CUSTOM

EGLFS_DEVICE_INTEGRATION = eglfs_kms_egldevice

include(../common/linux_arm_device_post.conf)
load(qt_config)
