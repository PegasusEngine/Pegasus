////---------------------------------------------------------------------
// geometry test block
// by Kleber Garcia
////---------------------------------------------------------------------

/// Extern input block from property grid
extern SphereRadius = 0.45;
extern CubeRotAxis = float3(0.3, 0.4, 0.01);
extern SphereLayerCount = 4;
extern Material_GlitterIntensity = 0.07;
extern Material_GlitterRepetition = float2( 20.1, 20.1 );
/// extern input block end
#define MAX_SPHERES 10


gDegreeArray = static_array<int[MAX_SPHERES]>;

i = 0; while(i < MAX_SPHERES)
{
	gDegreeArray[i] = ((i + 1) % 3) + 1;
	i = i + 1;
}

struct StateStruct
{
	transform : float4x4;
	proj : float4x4;
};

struct SpeakerStateStruct
{
	time : float;
	glitterIntensity : float;
	glitterRepetition : float2;
};

////---------------------------------------------------------------------

///// LOAD PROGRAMS /////
blockProgram = LoadProgram("Programs/CubeProgram.pas");
discoSpeakerProgram = LoadProgram("Programs/discospeaker.pas");
blurHorizontalProgram = LoadProgram("Programs/blurhorizontal.pas");
blurVerticalProgram = LoadProgram("Programs/blurvertical.pas");
compositeProgram = LoadProgram("Programs/composite.pas");
/////////////////////////

////---------------------------------------------------------------------

//// SETUP UNIFORMS /////
cubeTex = GetUniformLocation(blockProgram, "cubeTex");
uniformState = GetUniformLocation(blockProgram, "uniformState");
uniformStateBuffer = CreateUniformBuffer(sizeof(StateStruct));
speakerUniformState = GetUniformLocation(discoSpeakerProgram, "uniformState");
speakerUniformStateBuffer = CreateUniformBuffer(sizeof(SpeakerStateStruct));
compositeInput1 = GetUniformLocation(compositeProgram, "inputTexture1");
compositeInput2 = GetUniformLocation(compositeProgram, "inputTexture2");
horizontalInput = GetUniformLocation(compositeProgram, "inputTexture");
verticalInput = GetUniformLocation(compositeProgram,   "inputTexture");
/////////////////////////

////---------------------------------------------------------------------

extern CombTranslation = float3(0.0,0.0,0.0);
extern CombRotation = float4(0.0,0.0,0.0,0.0);
extern CombScale = float3(1.0,1.0,1.0);

//// SETUP MESHES ////
cubeMesh = CreateMesh();


boxGenerator = CreateMeshGenerator("BoxGenerator");
boxGenerator.CubeExtends = float3(10.0,10.0,40.50);
boxGenerator.FaceSubdivision = 45;

waveOperator = CreateMeshOperator("WaveFieldOperator");
waveOperator.WFFreqAmpOffset0 = float3(3.0,0.8,0.0);
waveOperator.WFFreqAmpOffset1 = float3(1.0,2.0,0.0);
waveOperator.WFFreqAmpOffset2 = float3(2.0,0.1,0.0);
waveOperator.WFFreqAmpOffset3 = float3(0.7,2.0,0.0);
waveOperator->AddGeneratorInput(boxGenerator);

boxGenerator2 = CreateMeshGenerator("BoxGenerator");
boxGenerator2.CubeExtends = float3(3.0,2.0,0.50);
boxGenerator2.FaceSubdivision = 8;
multiCopy = CreateMeshOperator("MultiCopyOperator");
multiCopy->AddGeneratorInput(boxGenerator2);
multiCopy.MultiCopyFactor = 16;
multiCopy.TranslationOffset = float3(1.0,8.0,0.0);
multiCopy.ScaleOffset = float3(0.7,0.7,0.7);
multiCopy.QuaternionRotOffset = float4(8.0,1.0,0.0,2.0);
combineOperator = CreateMeshOperator("CombineTransformOperator");
combineOperator->AddOperatorInput(waveOperator);
combineOperator->AddOperatorInput(multiCopy);

cylinder = CreateMeshGenerator("CylinderGenerator");
cylinder.CylinderCapRadius = 2.0;
cylinder.CylinderHeight = 8.0;
cylinder.CylinderRingCuts = 10;
cylinder.CylinderFaceCount = 56;
cubeMesh->SetGeneratorInput(cylinder);
//cubeMesh->SetOperatorInput(combineOperator);

quad = CreateMesh();
quad->SetGeneratorInput(CreateMeshGenerator("QuadGenerator"));

sphereMeshes = static_array<Mesh[MAX_SPHERES]>;

//TODO: Create a c style for loop for convenience
i = 0; while (i < MAX_SPHERES)
{
	sphereMeshes[i] = CreateMesh();
	icosph = CreateMeshGenerator("IcosphereGenerator");
	icosph.Degree = gDegreeArray[i];
	icosph.Radius = SphereRadius;
	sphereMeshes[i]->SetGeneratorInput(icosph);
	i = i + 1;
}


//// SETUP RENDER TARGETS ////
cubeFaceTarget = CreateRenderTarget( RenderTargetConfig(512, 512, FORMAT_RGBA_8_UNORM) );

tmpConfig = RenderTargetConfig(1280, 720, FORMAT_RGBA_8_UNORM);
tempTarget1 = CreateRenderTarget( tmpConfig );

tmpConfig.Height = tmpConfig.Height / 4;
tmpConfig.Width = tmpConfig.Width / 4;
tempTarget2 = CreateRenderTarget( tmpConfig );
tempTarget3 = CreateRenderTarget( tmpConfig );

//////////////////////

////---------------------------------------------------------------------

//// SETUP RASTER STATES ////


/////////////////////////////

////---------------------------------------------------------------------
gState = StateStruct();
gSpeakerState = SpeakerStateStruct();


int Timeline_Update(info : UpdateInfo)
{
	combineOperator.Translation0 = CombTranslation;
	combineOperator.QuaternionRot0 = CombRotation;
 //TODO: add function for identity
	gState.transform = float4x4(
	1.0, 0.0, 0.0, 0.0,
	0.0, 1.0, 0.0, 0.0,
	0.0, 0.0, 1.0, 0.0,
	0.0, 0.0, 0.0, 1.0
	);

	gSpeakerState.time = info.beat;
	gSpeakerState.glitterIntensity = Material_GlitterIntensity;
	gSpeakerState.glitterRepetition = Material_GlitterRepetition;
}

int Timeline_Render(info : RenderInfo)
{
    viewportWidth = info.viewportWidth; 
    viewportHeight = info.viewportHeight;
    aspect = info.aspect; //w contains inverted aspect ratio.


    ///////////////////////////////////////////////
    //******** PASS 1: texture of box ***********//
    ///////////////////////////////////////////////
    // rendering to the face of the disco a normal map and a detail map
    SetViewport(cubeFaceTarget);
    SetRenderTarget(cubeFaceTarget); //use the fractal surface, no depth
    Clear(1,0,0); //clear the surface
    SetProgram(discoSpeakerProgram); // dispatch the shader
    SetMesh(quad); // screen space
    SetBuffer(speakerUniformStateBuffer, gSpeakerState);
    SetUniformBuffer(speakerUniformState, speakerUniformStateBuffer);

    Draw();

    ///////////////////////////////////////////////
    //******** PASS 2: scene          ***********//
    ///////////////////////////////////////////////

	gState.proj = GetProjection(1.2,aspect,0.01,500.0);

    SetViewport(tempTarget1);
    SetRenderTarget(tempTarget1);
    Clear(1, 0, 0); //clear the surface
    SetProgram(blockProgram);

    rotAxis = CubeRotAxis;

	
    gState.transform = GetRotation(rotAxis,  info.beat * 0.92);
	gState.transform[2][3] = -10;


    // do aspect ratio correctness on y axis
	//TODO: write +=, *= etc operators


    SetBuffer(uniformStateBuffer, gState);
    SetUniformBuffer(uniformState, uniformStateBuffer);
    SetUniformTextureRenderTarget(cubeTex, cubeFaceTarget);

	SetMesh(cubeMesh);
    Draw();
    i = 0; while (i < MAX_SPHERES)
    {
        SetMesh(sphereMeshes[i]);

        fi = 2.0 * ((float)i / (float)MAX_SPHERES) - 1.0;
        
        maxDepth = SphereLayerCount;
        j = 0; while(j < maxDepth)
        {
            fj = 2.0 * ((float)j / (float)maxDepth) - 1.0; 
            ox = 2.7*cos(info.beat * 0.6 + 4.7*fj * fi);
            oy = sin(info.beat * 0.6 - 6.0*fj + 3.0*fi);
            gState.transform[0][3] = 6.0 * fi + ox;
            gState.transform[1][3] = 6.0 * fj + oy;
            gState.transform[2][3] = -10.5;
            gState.transform[3][3] = 1.0;
            SetBuffer(uniformStateBuffer, gState);
			Draw();
			j = j + 1;
        }
		i = i + 1;
    }

    ///////////////////////////////////////////////
    //******** PASS 3: horizontal blur***********//
    ///////////////////////////////////////////////
    SetViewport(tempTarget2);
	SetRenderTarget(tempTarget2);
    SetProgram(blurHorizontalProgram);
    SetMesh(quad);
    SetUniformTextureRenderTarget(horizontalInput, tempTarget1);
    Draw();
    
    ///////////////////////////////////////////////
    //******** PASS 4: vertical   blur***********//
    ///////////////////////////////////////////////
    SetRenderTarget(tempTarget3);
    SetProgram(blurVerticalProgram);
    SetMesh(quad);
    SetUniformTextureRenderTarget(verticalInput, tempTarget2);
    Draw();

    ///////////////////////////////////////////////
    //******** PASS 5: final compositing ********//
    ///////////////////////////////////////////////
    SetViewport(Viewport(0,0,viewportWidth, viewportHeight));
    SetDefaultRenderTarget();
    SetProgram(compositeProgram);
    SetMesh(quad);
    SetUniformTextureRenderTarget(compositeInput1, tempTarget1); //original scene
    SetUniformTextureRenderTarget(compositeInput2, tempTarget3); //bloomed scene
    Draw();


	UnbindPixelResources();
}

